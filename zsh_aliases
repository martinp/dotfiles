# -*- mode: sh -*-

function cond-alias {
    # Split on = and extract 1st word (alias name) and 2nd word (alias value)
    local -r name=${1[(ws:=:)1]}
    local -r value=${1[(ws:=:)2]}
    # Command is the first word of the alias value
    local cmd=${value[(w)1]}
    # If command match any of the below, the actual command is the second word
    case "$cmd" in
        sudo|noglob|cd|cat)
            # Remove $( and <( prefixes from command
            cmd=${value[(w)2]}
            cmd=${cmd:s/$\(//}
            cmd=${cmd:s/<\(//}
            ;;
    esac
    if whence $cmd > /dev/null; then
        alias $name=$value
    fi
}

cond-alias aptup='sudo apt update && sudo apt upgrade'
cond-alias brew-fzf='cat <(brew formulae) <(brew casks | sed "s/^/--cask /") | fzf'
cond-alias curl='noglob curl'
cond-alias ec='emacsclient -nq'
cond-alias find='noglob find'
cond-alias git-root='cd $(git rev-parse --show-toplevel)'
cond-alias grep='grep --color=auto'
cond-alias hstat="history 0 | awk '{print \$2}' | sort | uniq -c | sort -nr | head"
cond-alias mg='mg -n'
cond-alias ta='tmux new-session -AD -s $LOGNAME'
cond-alias week='date +%V'

# Create one or more directories and change to the last one
function take {
    mkdir -p $@ && cd ${@:$#}
}

# Alias diff
function alias-diff {
    local -r diff_path="$(whence diff)"
    if [[ "$OSTYPE" == "linux-gnu" || "$diff_path" == "/usr/local/bin/diff" ]]; then
        # GNU diff, which supports --color
        alias diff='diff --color=auto'
    fi
}

alias-diff

# Alias ls
function alias-ls {
    local -r ls_opts='--group-directories-first --color=auto'

    # Always prefer exa
    if (( $+commands[exa] )); then
        alias ls="exa ${ls_opts}"
        alias ll="exa ${ls_opts} --git --group --long"
        alias tree="exa ${ls_opts} --tree"
        return
    fi

    # Fallback
    case "$OSTYPE" in
        darwin*|freebsd*)
            if (( $+commands[gls] )); then
                alias ls="gls ${ls_opts}"
                alias ll="gls ${ls_opts} -lh"
            elif (( $+commands[gnuls] )); then
                alias ls="gnuls ${ls_opts}"
                alias ll="gnuls ${ls_opts} -lh"
            else
                alias ls='ls -G'
                alias ll='ls -Glh'
            fi
            ;;
        *)
            alias ls="ls ${ls_opts}"
            alias ll="ls ${ls_opts} -lh"
            ;;
    esac
}

alias-ls

# Activate, deactivate or switch virtualenv
function venv {
    local -r venv="$(realpath "$PWD/venv")"
    if [[ -d "$venv" ]]; then
        if [[ -n "$VIRTUAL_ENV" && "$VIRTUAL_ENV" != "$venv" ]]; then
            echo "venv: deactivating $VIRTUAL_ENV" 1>&2
            deactivate
        fi
        echo "venv: activating $venv"
        source "${venv}/bin/activate"
    else
        if [[ -n "$VIRTUAL_ENV" ]]; then
            echo "venv: deactivating $VIRTUAL_ENV" 1>&2
            deactivate
        else
            echo "venv: $venv is not a directory"
            return 1
        fi
    fi
}

# Local aliases
[[ -s "$HOME/.zsh_aliases.local" ]] && source "$HOME/.zsh_aliases.local"

# Clean up functions
unfunction cond-alias alias-diff alias-ls
