function alias-if-in-path {
    # Split on = and extract 1st word (alias name) and 2nd word (alias value)
    local -r name=${1[(ws:=:)1]}
    local -r value=${1[(ws:=:)2]}
    # Command is the first word of the alias value
    local cmd=${value[(w)1]}
    # If command match any of the below, the actual command is the second word
    case "$cmd" in
        sudo|noglob|cd)
            # Remove substitution from command
            cmd=${value[(w)2]:s/$\(//}
            ;;
    esac
    (( $+commands[$cmd] )) && alias $name=$value
}

alias-if-in-path grep='grep --color=auto'
alias-if-in-path aptup='sudo apt update && sudo apt upgrade'
alias-if-in-path autossh='autossh -M 0 -o "ServerAliveInterval 10"'
alias-if-in-path diff='colordiff'
alias-if-in-path ec='emacsclient -nq'
alias-if-in-path mg='mg -n'
alias-if-in-path lgrep='ls | grep -i'
alias-if-in-path git-root='cd $(git rev-parse --show-toplevel)'
alias-if-in-path week='date +%V'
alias-if-in-path cat='bat'
alias-if-in-path find='noglob find'
alias-if-in-path curl='noglob curl'

function rmt {
    mv "$@" "$HOME/.Trash"
}

# Alias ls
_ls_opts='--group-directories-first --color=auto'
case "$OSTYPE" in
    darwin*|freebsd*)
        if (( $+commands[exa] )); then
            alias ls="exa ${_ls_opts}"
            alias ll="exa ${_ls_opts} --git -l"
            alias tree="exa ${_ls_opts} --tree"
        elif (( $+commands[gls] )); then
            alias ls="gls ${_ls_opts}"
            alias ll="gls ${_ls_opts} -lh"
        elif (( $+commands[gnuls] )); then
            alias ls="gnuls ${_ls_opts}"
            alias ll="gnuls ${_ls_opts} -lh"
        else
            alias ls='ls -G'
            alias ll='ls -Glh'
        fi
        ;;
    *)
        alias ls="ls ${_ls_opts}"
        alias ll="ls ${_ls_opts} -lh"
esac
unset _ls_opts

# Clean up functions
unfunction alias-if-in-path
[[ ! -d "$HOME/.Trash" ]] && unfunction rmt
