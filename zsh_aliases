# -*- mode: sh -*-

function alias-if-in-path {
    # Split on = and extract 1st word (alias name) and 2nd word (alias value)
    local -r name=${1[(ws:=:)1]}
    local -r value=${1[(ws:=:)2]}
    # Command is the first word of the alias value
    local cmd=${value[(w)1]}
    # If command match any of the below, the actual command is the second word
    case "$cmd" in
        sudo|noglob|cd|cat)
            # Remove $( and <( prefixes from command
            cmd=${value[(w)2]}
            cmd=${cmd:s/$\(//}
            cmd=${cmd:s/<\(//}
            ;;
    esac
    (( $+commands[$cmd] )) && alias $name=$value
}

alias-if-in-path aptup='sudo apt update && sudo apt upgrade'
alias-if-in-path brew-fzf='cat <(brew formulae) <(brew casks | sed "s/^/--cask /") | fzf'
alias-if-in-path curl='noglob curl'
alias-if-in-path ec='emacsclient -nq'
alias-if-in-path find='noglob find'
alias-if-in-path git-root='cd $(git rev-parse --show-toplevel)'
alias-if-in-path grep='grep --color=auto'
alias-if-in-path mg='mg -n'
alias-if-in-path ta='tmux new-session -AD -s $LOGNAME'
alias-if-in-path week='date +%V'

# Create one or more directories and change to the last one
function take {
    mkdir -p $@ && cd ${@:$#}
}

# Alias diff
function alias-diff {
    local -r diff_path="$(whence diff)"
    if [[ "$OSTYPE" == "linux-gnu" || "$diff_path" == "/usr/local/bin/diff" ]]; then
        # GNU diff, which supports --color
        alias diff='diff --color=auto'
    fi
}

alias-diff

# Alias ls
function alias-ls {
    local -r ls_opts='--group-directories-first --color=auto'

    # Always prefer exa
    if (( $+commands[exa] )); then
        alias ls="exa ${ls_opts}"
        alias ll="exa ${ls_opts} --git -l"
        alias tree="exa ${ls_opts} --tree"
        return
    fi

    # Fallback
    case "$OSTYPE" in
        darwin*|freebsd*)
            if (( $+commands[gls] )); then
                alias ls="gls ${ls_opts}"
                alias ll="gls ${ls_opts} -lh"
            elif (( $+commands[gnuls] )); then
                alias ls="gnuls ${ls_opts}"
                alias ll="gnuls ${ls_opts} -lh"
            else
                alias ls='ls -G'
                alias ll='ls -Glh'
            fi
            ;;
        *)
            alias ls="ls ${ls_opts}"
            alias ll="ls ${ls_opts} -lh"
            ;;
    esac
}

alias-ls

# Local aliases
[[ -s "$HOME/.zsh_aliases.local" ]] && source "$HOME/.zsh_aliases.local"

# Clean up functions
unfunction alias-if-in-path alias-diff alias-ls
