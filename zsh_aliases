function alias-if-in-path {
    # Split on = and extract 1st word (alias name) and 2nd word (alias value)
    local -r name=${1[(ws:=:)1]}
    local -r value=${1[(ws:=:)2]}
    # Command is the first word of the alias value
    local cmd=${value[(w)1]}
    # If command match any of the below, the actual command is the second word
    case "$cmd" in
        sudo|noglob|cd)
            # Remove substitution from command
            cmd=${value[(w)2]:s/$\(//}
            ;;
    esac
    (( $+commands[$cmd] )) && alias $name=$value
}

alias-if-in-path aptup='sudo apt update && sudo apt upgrade'
alias-if-in-path autossh='autossh -M 0 -o "ServerAliveInterval 10"'
alias-if-in-path diff='colordiff'
alias-if-in-path ec='emacsclient -nq'
alias-if-in-path mg='mg -n'
alias-if-in-path lgrep='ls | grep -i'
alias-if-in-path git-root='cd $(git rev-parse --show-toplevel)'
alias-if-in-path week='date +%V'
alias-if-in-path cat='bat'
alias-if-in-path find='noglob find'
alias-if-in-path curl='noglob curl'
alias-if-in-path rmt='trash'

# Alias ls
function alias-ls {
    local -r ls_opts='--group-directories-first --color=auto'

    # Always prefer exa
    if (( $+commands[exa] )); then
        alias ls="exa ${ls_opts}"
        alias ll="exa ${ls_opts} --git -l"
        alias tree="exa ${ls_opts} --tree"
        return
    fi

    # Fallback
    case "$OSTYPE" in
        darwin*|freebsd*)
            if (( $+commands[gls] )); then
                alias ls="gls ${ls_opts}"
                alias ll="gls ${ls_opts} -lh"
            elif (( $+commands[gnuls] )); then
                alias ls="gnuls ${ls_opts}"
                alias ll="gnuls ${ls_opts} -lh"
            else
                alias ls='ls -G'
                alias ll='ls -Glh'
            fi
            ;;
        *)
            alias ls="ls ${ls_opts}"
            alias ll="ls ${ls_opts} -lh"
    esac
}

alias-ls

# Clean up functions
unfunction alias-if-in-path alias-ls
